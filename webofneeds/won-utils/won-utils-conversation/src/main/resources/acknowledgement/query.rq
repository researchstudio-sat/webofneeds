PREFIX mod: <https://w3id.org/won/modification#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <https://w3id.org/won/message#>
PREFIX won: <https://w3id.org/won/core#>
PREFIX rdfg: <http://www.w3.org/2004/03/trix/rdfg-1/>

DELETE {
  GRAPH ?contentGraph { ?s ?p ?o }.
}
WHERE
{
  {
    # remove all messages that have remote messages (except responses)
    GRAPH <urn:x-arq:UnionGraph> {
      ?m msg:content ?contentGraph;
         msg:messageType ?mtype;
         msg:correspondingRemoteMessage ?rm .
         filter (?mtype in (msg:ConnectMessage, msg:OpenMessage, msg:CloseMessage, msg:ConnectionMessage))
         # unless there are the required SuccessResponses
        FILTER NOT EXISTS {
          ?sm msg:isResponseTo ?m.
          ?sm msg:previousMessage+ ?m.
          ?sm msg:messageType msg:SuccessResponse.
          ?rsm msg:isRemoteResponseTo ?m.
          ?rsm msg:isResponseTo ?rm.
          ?rsm msg:previousMessage+/msg:correspondingRemoteMessage ?m.
          ?rsm msg:messageType msg:SuccessResponse.
          ?rsm msg:correspondingRemoteMessage ?lsm.
          ?lsm msg:previousMessage+ ?m.
        }
    }
  } union {
    # remove all messages that do not require remote messages
    GRAPH <urn:x-arq:UnionGraph> {
      ?m msg:content ?contentGraph;
         msg:messageType ?mtype.
      filter (?mtype  in (msg:CreateMessage, msg:ActivateMessage, msg:DeactivateMessage, msg:HintFeedbackMessage))
      # unless there is the required SuccessResponse
      FILTER NOT EXISTS {
        ?sm msg:isResponseTo ?m.
        ?sm msg:previousMessage+ ?m.
        ?sm msg:messageType msg:SuccessResponse.
      }
    }
  } union {
    # remove all messages that require remote but don't have any (except responses)
    GRAPH <urn:x-arq:UnionGraph> {
      ?m msg:content ?contentGraph;
         msg:messageType ?mtype;
         filter (?mtype  in (msg:ConnectMessage, msg:OpenMessage, msg:CloseMessage, msg:ConnectionMessage))
         optional {
           ?m msg:correspondingRemoteMessage ?rm .
         }
         filter (!bound(?rm))
    }
  }
  GRAPH ?contentGraph {
      ?s ?p ?o .
  }
}