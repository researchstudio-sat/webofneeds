PREFIX mod: <https://w3id.org/won/modification#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <https://w3id.org/won/message#>
PREFIX won: <https://w3id.org/won/core#>

CONSTRUCT { ?retractingMsg mod:retracts ?retractedMsg . }
WHERE
 {
    GRAPH ?retractingMessageEnvelope {
         ?retractingMsg msg:content ?retractingMessageContent
    }
    GRAPH ?retractingMessageContent {
        ?retractingMsg mod:retracts ?retractedMsg .
        ?rs ?rp ?ro .
    }
    GRAPH ?e2 {
        ?retractingMsg msg:sender ?sender
    }
    GRAPH ?e3 {
        ?retractedMsg msg:sender ?sender
    }
    # Note: for this pattern, we need the union of all graphs that contain msg:previousMessage triples,
    # Jena provides the special graph name <urn:x-arq:UnionGraph> for querying the union of all the query's graphs
    # THIS QUERY WILL ONLY WORK WITH JENA
    GRAPH <urn:x-arq:UnionGraph> {
        ?retractingMsg msg:previousMessage+ ?retractedMsg
    }
    GRAPH ?retractedMessageEnvelope {
        ?retractedMsg msg:content ?retractedMessageContent
    }
 }
