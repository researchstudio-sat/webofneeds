PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <https://w3id.org/won/message#>
PREFIX won: <https://w3id.org/won/core#>
PREFIX agr: <https://w3id.org/won/agreement#>
PREFIX mod: <https://w3id.org/won/modification#> 

SELECT distinct ?msg ?direction ?msgType ?senderAtom ?retracts ?accepts ?proposes ?proposesToCancel ?rejects ?previous ?correspondingRemoteMessage ?isResponseTo ?isRemoteResponseTo ?contentGraph WHERE 
{
    GRAPH <urn:x-arq:UnionGraph> 
    {
       {
        ?msg a msg:FromExternal.
        bind(msg:FromExternal as ?direction)
       } union {
        ?msg a msg:FromOwner.
        bind(msg:FromOwner as ?direction)
       } union {
        ?msg a msg:FromSystem.
        bind(msg:FromSystem as ?direction)
       } OPTIONAL {
        ?msg msg:previousMessage ?previous.
       } OPTIONAL {
        ?msg msg:isResponseTo ?isResponseTo.
       } OPTIONAL {
        ?msg msg:isRemoteResponseTo ?isRemoteResponseTo.
       } OPTIONAL {
        ?msg msg:correspondingRemoteMessage ?correspondingRemoteMessage.
       } OPTIONAL {
        ?msg agr:proposes ?proposes.
       } OPTIONAL {
        ?msg agr:rejects ?rejects.
       } OPTIONAL {
        ?msg agr:proposesToCancel ?proposesToCancel.
       } OPTIONAL {
        ?msg agr:accepts ?accepts.
       } OPTIONAL {
        ?msg mod:retracts ?retracts.
       } OPTIONAL {
        ?msg msg:senderAtom ?senderAtom.
       } OPTIONAL {
        ?msg msg:messageType ?msgType.
       } OPTIONAL {
        ?msg msg:content ?contentGraph.
       }
    }  
}