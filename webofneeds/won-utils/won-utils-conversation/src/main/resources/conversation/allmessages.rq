PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <https://w3id.org/won/message#>
PREFIX won: <https://w3id.org/won/core#>
SELECT distinct ?first ?msg ?distance ?text ?msgType ?time ?rem WHERE {
 {
   SELECT distinct ?first ?msg (count (?mid) as ?distance) WHERE {
     ?msg msg:previousMessage* ?mid .
     ?mid msg:previousMessage+ ?first .
     FILTER NOT EXISTS {?first msg:previousMessage ?none}            
   }
   GROUP BY ?msg ?first 
 }
 OPTIONAL {
   ?msg won:textMessage ?text.
   ?msg msg:messageType ?msgType.
 }
 OPTIONAL {
    ?msg msg:correspondingRemoteMessage ?rem . 
    ?rem won:textMessage ?text.
    ?rem msg:messageType ?msgType.
 }
 ?msg msg:receivedTimestamp ?time.  
} ORDER BY ?first ?distance ?time