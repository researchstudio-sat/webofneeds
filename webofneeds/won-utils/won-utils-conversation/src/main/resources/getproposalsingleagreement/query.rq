# proposal4accept
PREFIX mod: <https://w3id.org/won/modification#>
PREFIX agr: <https://w3id.org/won/agreement#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <https://w3id.org/won/message#>
PREFIX won: <https://w3id.org/won/core#>

CONSTRUCT { ?prop agr:proposes ?clause . }
 WHERE {
   
     filter (?prop != ?clause) .
     filter (?prop != ?targetedacceptsmessage) .
    
    GRAPH ?g1 {   
        ?prop agr:proposes ?clause .
    }
   
    GRAPH ?g2 {
        ?targetedacceptsmessage agr:accepts ?prop .
    }
          
   GRAPH ?clauseContent {
      ?s ?p ?o .
    }

   GRAPH ?g3 {
        ?clause msg:content ?clauseContent .
    }
    

        
    # Note: for this pattern, we need the union of all graphs that contain msg:previousMessage triples,
    # Jena provides the special graph name <urn:x-arq:UnionGraph> for querying the union of all the query's graphs
    # THIS QUERY WILL ONLY WORK WITH JENA
      GRAPH <urn:x-arq:UnionGraph> {
        ?prop msg:previousMessage+/msg:correspondingRemoteMessage? ?clause .
        ?targetedacceptsmessage msg:previousMessage+/msg:correspondingRemoteMessage ?prop .    
     }
}