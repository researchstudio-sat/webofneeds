prefix dc:    <http://purl.org/dc/elements/1.1/>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix geo:   <http://www.w3.org/2003/01/geo/wgs84_pos#>
prefix conn:  <http://localhost:8080/won/resource/connection/>
prefix event: <http://localhost:8080/won/resource/event/>
prefix woncrypt: <http://purl.org/webofneeds/woncrypt#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix local: <http://localhost:8080/won/resource/>
prefix won:   <http://purl.org/webofneeds/model#>
prefix msg:   <http://purl.org/webofneeds/message#>
prefix signature: <http://icp.it-risk.iwvi.uni-koblenz.de/ontologies/signature.owl#>
prefix ldp:   <http://www.w3.org/ns/ldp#>

# each signature graph should have required signature triples, including one link to envelope or content graph

#ASK
#{
  SELECT

    ?numSignatures
    ?numCorrectPropSignatures
    ?numSignaturesTriples


    (if(?numCorrectPropSignatures != ?numSignatures || (?numSignatures*11 != ?numSignaturesTriples), "FAIL", "OK") AS ?check)

  WHERE
  {


    # count number of signature graphs that have required properties
    { SELECT (count(?g) as ?numCorrectPropSignatures)
        WHERE
        {
        GRAPH ?g
            {
              ?g rdf:type signature:Signature .
              ?g signature:hasSignatureValue ?svalue .
              ?g signature:hasVerificationCertificate ?signer .
              ?g msg:hasSignedGraph ?signed .

              ?g signature:hasGraphSigningMethod ?smethod .
              ?smethod rdf:type signature:GraphSigningMethod .
              ?smethod signature:hasDigestMethod ?digest .
              ?smethod signature:hasGraphCanonicalizationMethod ?canon .
              ?smethod signature:hasGraphDigestMethod ?gdigest .
              ?smethod signature:hasGraphSerializationMethod ?gserial .
              ?smethod signature:hasSignatureMethod ?sign .

            } .

        GRAPH ?signed
            {
                FILTER NOT EXISTS
                    {  ?signed rdf:type signature:Signature . }
            } .
        }
        #GROUP BY ?g
        #HAVING (count(?signed) = 1)
     }


     # count number of signature graphs
    {  SELECT (count(?g) as ?numSignatures) WHERE
     {
       GRAPH ?g
       {
               ?g rdf:type signature:Signature .
       } .
     }
    }


    # count number of signature triples
    { SELECT (count(*) as ?numSignaturesTriples)
        WHERE
        {
        GRAPH ?g
            {
              ?a rdf:type signature:Signature .
              ?b ?c ?d .
            } .
        }
        #GROUP BY ?g
        #HAVING (count(?signed) = 1)
     }


  }







#}