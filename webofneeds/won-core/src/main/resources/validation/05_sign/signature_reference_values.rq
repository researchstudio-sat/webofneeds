prefix dc:    <http://purl.org/dc/elements/1.1/>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix geo:   <http://www.w3.org/2003/01/geo/wgs84_pos#>
prefix conn:  <http://localhost:8080/won/resource/connection/>
prefix event: <http://localhost:8080/won/resource/event/>
prefix woncrypt: <http://purl.org/webofneeds/woncrypt#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix local: <http://localhost:8080/won/resource/>
prefix won:   <http://purl.org/webofneeds/model#>
prefix msg:   <http://purl.org/webofneeds/message#>
prefix signature: <http://icp.it-risk.iwvi.uni-koblenz.de/ontologies/signature.owl#>
prefix ldp:   <http://www.w3.org/ns/ldp#>

# all values in signature references must be identical to the values in the respective signatures


#ASK
#{
  SELECT

    ?numSignatureRefs
    ?numCorrectSignatureRefs


    (if(?numSignatureRefs != ?numCorrectSignatureRefs, "FAIL", "OK") AS ?check)

  WHERE
  {


    # count number of signature references that have values identical to respective signature values
    { SELECT (count(?signature) as ?numCorrectSignatureRefs)
        WHERE
        {
        GRAPH ?env
            {
              #?env rdf:type msg:EnvelopeGraph .
              #?event rdfg:subGraphOf ?event .

              ?event msg:referencesSignature ?ref .
              ?ref msg:hasSignatureGraph ?signature .
              ?ref msg:hasSignatureValue ?svalue .
              #?ref msg:hasSignedGraph ?signed .
            } .

        GRAPH ?signature
            {
              ?signature rdf:type signature:Signature .
              #?signature msg:hasSignedGraph ?signed .
              ?signature signature:hasSignatureValue ?svalue .
            } .
        }
     }


    # count number of signature references (inside this dataset. references outside the dataset are not checked here)
    { SELECT (count(?ref) as ?numSignatureRefs)
        WHERE
        {
        GRAPH ?env
            {
              #?env rdf:type msg:EnvelopeGraph .
              #?event rdfg:subGraphOf ?event .

              ?event msg:referencesSignature ?ref .
              ?ref msg:hasSignatureGraph ?signature .
              #?ref msg:hasSignatureValue ?svalue .
              #?ref msg:hasSignedGraph ?signed .
            } .
         #just check if the referenced graph is in the dataset, do not count the reference otherwise
         GRAPH ?signature {?signature a signature:Signature}
        }
     }

  }
#}