prefix dc:    <http://purl.org/dc/elements/1.1/>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix geo:   <http://www.w3.org/2003/01/geo/wgs84_pos#>
prefix conn:  <http://localhost:8080/won/resource/connection/>
prefix event: <http://localhost:8080/won/resource/event/>
prefix woncrypt: <http://purl.org/webofneeds/woncrypt#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix local: <http://localhost:8080/won/resource/>
prefix won:   <http://purl.org/webofneeds/model#>
prefix msg:   <http://purl.org/webofneeds/message#>
prefix signature: <http://icp.it-risk.iwvi.uni-koblenz.de/ontologies/signature.owl#>
prefix ldp:   <http://www.w3.org/ns/ldp#>
prefix rdfg:   <http://www.w3.org/2004/03/trix/rdfg-1/>

# any named graph in D should be either a signature graph or a signed graph.

#ASK
#{
  SELECT

  ?numGraphs
  ?numCorrectSignatures


    (if(?type = msg:HintMessage || (?numCorrectSignatures = 1 && ?numGraphs >= 2), "OK", "FAIL") AS ?check)

  WHERE
  {

    # allow hint messages that are not signed
    {SELECT ?type
      WHERE
      {
          graph ?g{
              ?g rdfg:subGraphOf ?event.
                   ?event msg:hasMessageType ?type.
                 ?event a msg:FromExternal
          }
        }
    }

    # count number of signature graphs that have corresponding signed graph
    { SELECT (count(distinct ?g) as ?numCorrectSignatures)
        WHERE
        {
        GRAPH ?g
            {
              ?g rdf:type signature:Signature .
              ?g msg:hasSignedGraph ?signed .
            } .

        GRAPH ?signed
            {
               ?a ?b ?c
            } .
        }
     }


    # count number of graphs
    {  SELECT (count(DISTINCT ?g) as ?numGraphs) WHERE
     {
       GRAPH ?g
       {
               ?a ?b ?c .
       } .
     }
    }


  }
#}