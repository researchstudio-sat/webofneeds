PREFIX con: <https://w3id.org/won/content#>
PREFIX match: <https://w3id.org/won/matching#>
prefix won: <https://w3id.org/won/core#>
prefix rdfs: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?slice ?atom ?value WHERE { # value is the remote atom
    BIND('connection' as ?slice)
    ?connectionUri rdfs:type won:Connection.
    ?connectionUri won:crawlStatus 'DONE'.
    ?connectionUri won:crawlDate ?date.
    ?connectionUri won:sourceAtom ?atom.
    ?connectionUri won:targetAtom ?value.
    ?rating con:binaryRating con:Good.
    ?rating con:feedbackTarget ?connectionUri.
    OPTIONAL {?atom match:flag ?flag1}.
    OPTIONAL {?value match:flag ?flag2}.
    FILTER (!bound(?flag1) || (?flag1 != won:DoNotMatch && ?flag1 != match:UsedForTesting))
    FILTER (!bound(?flag2) || (?flag2 != won:DoNotMatch && ?flag2 != match:UsedForTesting))
    FILTER (?date >= ?from && ?date < ?to) # bind variables ?from and ?to here
}